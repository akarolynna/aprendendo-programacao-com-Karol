{
ğŸ“… Data de criaÃ§Ã£o: 12/11/2025  
ğŸ‘©â€ğŸ’» Autora: Ana Karolynna Mota  
ğŸ¯ Desafio: Sistema de CorreÃ§Ã£o AutomÃ¡tica de Provas  

ğŸ“˜ DescriÃ§Ã£o:
Crie um algoritmo que simule a **correÃ§Ã£o automÃ¡tica** de uma prova de mÃºltipla escolha com **5 questÃµes**, 
cujas alternativas possÃ­veis sÃ£o **A**, **B**, **C** ou **D**.  
O sistema deve permitir que o professor cadastre o **gabarito oficial** e, em seguida, realize a correÃ§Ã£o das provas dos alunos,
informando o **nome** e as **respostas** de cada um.

A cada aluno corrigido, o programa deve contabilizar o nÃºmero de acertos e exibir o desempenho correspondente.  
No final, o programa deve exibir:
         1. Quantos Alunos gabaritaram;
         2. Quantos Alunos acertaran entre 3 e 4
         3. Quantos Alunos acertaram entre 0 ou 1 questÃ£o.

ğŸ“Š ClassificaÃ§Ã£o dos Alunos:
+----------------------------+-----------------------------+
| ğŸ’¯ Desempenho              | ğŸ“ DescriÃ§Ã£o                 |
+----------------------------+-----------------------------+
| ğŸ… Gabaritou               | 5 acertos                   |
| ğŸ‘ Bom desempenho          | 3 a 4 acertos               |
| ğŸ“– ReforÃ§ar os estudos     | 0 a 2 acertos               |
+----------------------------+-----------------------------+

ğŸ’¡ Dica: Utilize **vetores** para armazenar o gabarito e as respostas dos alunos,
e **laÃ§os de repetiÃ§Ã£o** para permitir corrigir vÃ¡rias provas atÃ© o usuÃ¡rio decidir parar.
}

Algoritmo "semnome"

Var
   vetorRespostaAluno,vetorGabarito: vetor[1..5] de caractere
   aluno,desejaContinuar: caractere
   i, cont, qtdAlunosGabaritaram, qtdAcertaramTresOuQuatro, qtdAcertaramMinimo: inteiro

Inicio
   qtdAlunosGabaritaram:=0
   qtdAcertaramTresOuQuatro:=0
   qtdAcertaramMinimo:=0
   desejaContinuar:="sim"

   para i de 1 ate 5 faca
      repita
         escreva("Informe o gabarito da questÃ£o",i, ": ")
         leia(vetorGabarito[i])
         se (vetorGabarito[i] <> "A") e (vetorGabarito[i] <> "B") e (vetorGabarito[i] <> "C") e (vetorGabarito[i] <> "D") entao
            escreval("Gabarito invÃ¡lido! Digite apenas A, B, C ou D.")
         fimse
      ate (vetorGabarito[i] = "A") ou (vetorGabarito[i] = "B") ou (vetorGabarito[i] = "C") ou (vetorGabarito[i] = "D")
   fimpara

   escreval("- - - - - - - - - - - - - - - - ")

   enquanto desejacontinuar="sim" faca
      cont:=0
      escreva("Nome Aluno: ")
      leia(aluno)
      escreval("Qual foi a resposta que o(a) aluno(a) ",aluno, " respondeu na:")

      para i de 1 ate 5 faca
         escreva("QuestÃ£o ", i, ": ")
         leia(vetorRespostaAluno[i])
      fimpara
      escreval()

      para i de 1 ate 5 faca
         se vetorRespostaAluno[i] = vetorGabarito[i] entao
            cont:=cont +1
         fimse
      fimpara
      escreval(aluno, " PONTUAÃ‡ÃƒO: ", cont)


      se (cont = 5) entao
         escreval("STATUS: Gabaritou")
         qtdAlunosGabaritaram:= qtdAlunosGabaritaram + 1
      senao
         se (cont >= 3) e (cont <= 4) entao
            qtdAcertaramTresOuQuatro:=qtdAcertaramTresOuQuatro+1
            escreval("STATUS: Muito bem!")
         senao
            escreval("STATUS: Vamos reforÃ§ar os estudos?")
            qtdAcertaramMinimo:=qtdAcertaramMinimo+1
         fimse
      fimse
      escreval("- - - - - - - - - - - - - - - - ")
      escreva("Deseja continuar?")
      leia(desejaContinuar)
   fimenquanto

   escreval("- - - - - - - - - - - - - - - - ")
   escreval("Quantidade de Alunos que gabaritaram:", qtdAlunosGabaritaram)
   escreval("Quantidade de Alunos que acertaram entre 3 e 4  questÃµes:", qtdAcertaramTresOuQuatro)
   escreval("Quantidade de Alunos que acertaram entre 0 e 1 questÃ£o:", qtdAcertaramMinimo)


Fimalgoritmo


